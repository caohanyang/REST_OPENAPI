/*
*  first.jape
*
* Copyright (c) 1998-2004, The University of Sheffield.
*
*  This file is part of GATE (see http://gate.ac.uk/), and is free
*  software, licenced under the GNU Library General Public License,
*  Version 2, June 1991 (in the distribution as file licence.html,
*  and also available at http://gate.ac.uk/gate/licence.html).
*
*  Diana Maynard, 10 Sep 2001
*  Modified by Andreas Vlachidis February-March 2016
*  $Id: first.jape 17726 2014-03-21 15:10:00Z dgmaynard $
*/

Phase:	First
Input: Token NumberLetter
Options: control = appelt

// this has to be run first of all 

//////////////////////////////////////////////////////////////
Macro: SPACE
// space
// control
// space control
// control space

( 
 ({SpaceToken.kind == space}
  ({SpaceToken.kind == control})?
  ({SpaceToken.kind == control})?
 )
|
 ({SpaceToken.kind == control}
  ({SpaceToken.kind == control})?
  ({SpaceToken.kind == space})?
 )
)




///////////////////////////////////////////////////////////////

Rule: ClosedClass
// closed class words should not be part of names generally, so let's identify them
// rule modified for the purposes of CYMRIE - Andreas Vlachidis February 2016
Priority: 100

(
 {Token.category == DT}|
 {Token.category == PP}|
 {Token.category == PDT}|
 {Token.category == RP}|
 {Token.category == RB}|
 {Token.category == UH}|
 {Token.category == IN}
):tag
-->
:tag.ClosedClass = {rule = "ClosedClass"}

Rule: NumberLetter
Priority: 100
( 
 {NumberLetter}
):tag
-->
{} 


Rule: UpperAllCaps
Priority: 100
// separate proper nouns that are in all caps, as they're more ambiguous
(
 {Token.category == NNP, Token.orth == allCaps}
 ({Token.string == "-"}
  {Token.category == NNP, Token.orth == allCaps}
 )?
):tag
-->
:tag.Upper = {kind = "allCaps", rule = "Upper"}

Rule: Upper
// define what can be a possible proper noun - cater for the fact that POS tag might not be correct
// Rule modified for the purposes of CYMRIE, extract condition on upperInitial and mixedCaps matching
// due to the frequent use of verbs at the begining of a sentence. 
(
 ({Token.category == NNP}| 
   {Token.orth == upperInitial, Token.category =~"NN"}|
   {Token.orth == mixedCaps, Token.category =~"NN"} 
  )
 ({Token.string == "-"}
  {Token.category == NNP}
 )?
):tag
-->
:tag.Upper = {rule = "Upper"}



